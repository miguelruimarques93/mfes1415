class Date

instance variables
	day: nat1;
	month: nat1;
	year: nat;
	
operations
	public Date : nat1 * nat1 * nat ==> Date
	Date(d, m, y) == 
	(
		day := d;
		month := m;
		year := y;
		return self;
	)
	pre d <= 31 and
	m <= 12 and
	if m in set {4, 6, 9, 11}
	then d <= 30
	else (m = 2) => if isLeapYear(y) then (d <= 29) else (d <= 28);
	
	public getDay : () ==> nat1
	getDay() == (
		return self.day;
	);
	
	public getMonth : () ==> nat1
	getMonth() == (
		return self.month;
	);
	
	public getYear : () ==> nat
	getYear() == (
		return self.year;
	);
	
	public isLess : Date ==> bool
	isLess(other) == (
		return (self.year < other.year) or
					 (self.year = other.year and self.month < other.month) or
					 (self.year = other.year and self.month = other.month and self.day < other.day);
	);
	
	public isLessOrEqual : Date ==> bool
	isLessOrEqual(other) == (
		return self.isLess(other) or self.isEqual(other);
	);
	
	public isEqual: Date ==> bool
	isEqual(other) == (
		return self.year = other.year and self.month = other.month and self.day = other.day;
	);
	
	public isDifferent: Date ==> bool
	isDifferent(other) == (
		return not self.isEqual(other);
	);
	
	public isGreater : Date ==> bool
	isGreater(other) == (
		return self.isGreaterOrEqual(other) and self.isDifferent(other);
	);
	
	public isGreaterOrEqual : Date ==> bool
	isGreaterOrEqual(other) == (
		return not self.isLess(other);
	);
	
functions
	public static isLeapYear : nat -> bool
	isLeapYear(y) == (
		if y mod 100 = 0 then y mod 400 = 0 else y mod 4 = 0
	);	
end Date